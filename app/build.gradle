buildscript {
    ext.kotlin_version = '1.0.4'

    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
    jcenter()
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "dog.woofwoofinc.grace"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "1.0.0"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            debuggable true
            useProguard false
            minifyEnabled false
            shrinkResources false
        }

        beta {
            initWith(buildTypes.debug)
            applicationIdSuffix ".beta"
            versionNameSuffix "-beta"
        }

        release {
            debuggable false
            useProguard true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        warning 'InvalidPackage'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:support-annotations:24.2.1'

    compile('com.crashlytics.sdk.android:crashlytics:2.6.3@aar') {
        transitive = true;
    }
    compile('com.twitter.sdk.android:twitter:2.1.0@aar') {
        transitive = true;
    }

    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.2.0'
    compile 'io.reactivex:rxkotlin:0.60.0'
    compile 'com.github.jenzz:RxAppState:1.1.0'
    compile 'com.jakewharton.rxbinding:rxbinding-design-kotlin:0.4.0'

    compile 'com.pawegio.kandroid:kandroid:0.6.12@aar'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.3'
    compile 'com.tumblr:remember:1.0.0@aar'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'jp.wasabeef:picasso-transformations:2.1.0'

    testCompile 'junit:junit:4.12'
}
